
allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    afterEvaluate { project ->
        if (project.plugins.hasPlugin("com.android.application") || project.plugins.hasPlugin("com.android.library")) {
            if (project.android.namespace == null || project.android.namespace.isEmpty()) {
                project.android.namespace = project.group
                println "INFO: Set namespace for ${project.name} to ${project.group}"
            }
            project.android {
                compileSdk = 34
            }
        }
    }
}
subprojects {
    afterEvaluate { project ->
        if (project.plugins.hasPlugin("com.android.application") || project.plugins.hasPlugin("com.android.library")) {
            tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach { kotlinTask ->
                def sourceCompatibility = project.android.compileOptions.sourceCompatibility?.toString() ?: "1.8"
                def jvmTarget = kotlinTask.kotlinOptions.jvmTarget ?: ""
                if (sourceCompatibility != jvmTarget) {
                    kotlinTask.kotlinOptions.jvmTarget = sourceCompatibility
                    println "INFO: Updated jvmTarget for ${project.name} to ${sourceCompatibility}"
                }
            }
        }
    }
}
subprojects {
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
